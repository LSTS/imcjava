apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

repositories {
    mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir 'src/main'
			srcDir 'src/generated'
		}
	}
	
	generator {
		java {
			srcDir 'src/main'
			srcDir 'src/generated'
			srcDir 'src/generator'
		}
	}
}

jar {
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

dependencies {
	compile 'org.apache.commons:commons-lang3:3.1'
	generatorCompile 'org.apache.commons:commons-lang3:3.1'
	generatorRuntime 'org.apache.commons:commons-lang3:3.1'
}

task(generate, dependsOn: 'classes', type: JavaExec) {
   description 'Generate source code for IMC protocol'
   main 'pt.lsts.imc.generator.ClassGenerator'
   classpath sourceSets.generator.runtimeClasspath
   doFirst {
     if (project.hasProperty("IMC")) {
       args "$IMC"
     }
     else {
       ant.fail("You should pass the path to IMC repository as build parameter. Example: 'gradle generate -PIMC=../imc'")
     }
   }
}

